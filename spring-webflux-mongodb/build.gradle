plugins {
    id 'org.springframework.boot' version '2.5.3'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
    // gradle 7 부터는 org.asciidoctor.convert 대신 asciidoctor.jvm.convert 사용
    // adoc 파일 변환 후 build 디렉토리에 복사
    id "org.asciidoctor.jvm.convert" version "3.3.2"
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

repositories {
    mavenCentral()
}

/**
 * layered 옵션은 2.3.0 부터 deprecated 되고 기본값으로 활성화 되었다.
 * (https://docs.spring.io/spring-boot/docs/current/gradle-plugin/api/org/springframework/boot/gradle/tasks/bundling/BootJar.html#layered--)
 *
 * layered 옵션을 끄고 싶다면 아래 코드를 사용하면 된다.
 * (https://docs.spring.io/spring-boot/docs/current/gradle-plugin/reference/htmlsingle/#packaging-executable.configuring.layered-archives)
 */
//bootJar {
//    layered {
//        enabled = false
//    }
//}

configurations {
    asciidoctorExt
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-mongodb-reactive'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.mongodb:mongodb-driver-sync'
    compileOnly 'io.projectreactor.tools:blockhound:1.0.6.RELEASE'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'de.flapdoodle.embed:de.flapdoodle.embed.mongo'
    testImplementation 'io.projectreactor:reactor-test'

    // Spring Rest Docs 설정: https://docs.spring.io/spring-restdocs/docs/current/reference/html5/
    // asciidoctor 가 안되어서 추가 https://github.com/spring-projects/spring-restdocs/issues/680
    asciidoctorExt 'org.springframework.restdocs:spring-restdocs-asciidoctor'
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
    testImplementation 'org.springframework.restdocs:spring-restdocs-webtestclient'

//    testImplementation 'io.projectreactor.tools:blockhound-junit-platform:1.0.6.RELEASE'
}

test {
    useJUnitPlatform()
}

// 스니펫이 생성될 위치 지정: build/generated-snippets
ext {
    snippetsDir = file('build/generated-snippets')
}

// asciidoctor 태스크 구성
asciidoctor {
    configurations 'asciidoctorExt'

    // 입력 snippets 이 생성될 위치 지정
    inputs.dir snippetsDir
    // 문서가 만들어지기 전에 test 가 실행되도록 의존성 걸어준다
    dependsOn test
}

/**
 * /build/docs/asciidoc/index.html 이 생성되지 않는 이슈는 index.adoc 위치 문제
 * (https://blog.hodory.dev/2019/12/04/spring-rest-docs-with-gradle-not-working-html5/)
 *
 *
 */
bootJar {
    dependsOn asciidoctor

    // jar static 폴더에 생긴 HTML 파일을 /static/docs 에 넣어줌
    from ("build/docs/asciidoc/") {
        into 'src/main/resources/static/docs'
    }
}